/*
   C#예제 90-1 사물인터넷보드(wemos d1r1)을 웹서버로 동작시킨다음
   C#윈폼에서 GET방식으로 제어하기위한 녹칸다의 기본코드!
   C#윈폼에서 request를 하게되면 온도와 습도값을 1회측정해서 response한다!
*/

#include <ESP8266WiFi.h>
#include "DHT.h"

#define DHTPIN D3
#define DHTTYPE DHT11 

#ifndef STASSID
#define STASSID "~~~~" //WiFi 공유기의 ID
#define STAPSK  "~~~~" //WiFi 공유기의 PW
#endif

const char* ssid = STASSID;
const char* password = STAPSK;

WiFiServer server(80);
DHT dht(DHTPIN, DHTTYPE);

void setup() {
  Serial.begin(115200);
  dht.begin();
  
  // prepare LED
  pinMode(LED_BUILTIN, OUTPUT);
  digitalWrite(LED_BUILTIN, 0);

  // Connect to WiFi network
  Serial.println();
  Serial.println();
  Serial.print(F("Connecting to "));
  Serial.println(ssid);

  WiFi.mode(WIFI_STA);
  WiFi.begin(ssid, password);

  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(F("."));
  }
  Serial.println();
  Serial.println(F("WiFi connected"));

  // Start the server
  server.begin();
  Serial.println(F("Server started"));

  // Print the IP address
  Serial.println(WiFi.localIP());
}

void loop() {
  // Check if a client has connected
  WiFiClient client = server.available();
  if (!client) {
    return;
  }
  Serial.println(F("new client"));

  client.setTimeout(5000); // default is 1000

  // Read the first line of the request
  String req = client.readStringUntil('\r');
  Serial.println(F("request: "));
  Serial.println(req);
  
  while (client.available()) {
    client.read();
  }

  //데이터포멧 예시 : 온도/습도
  float h = dht.readHumidity();
  float t = dht.readTemperature();
  
  client.print(F("HTTP/1.1 200 OK\r\nContent-Type: text/plain\r\n\r\n"));
  client.print(String(t) + "/" + String(h));

  Serial.println(F("Disconnecting from client"));
}